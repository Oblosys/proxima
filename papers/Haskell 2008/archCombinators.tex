
\documentclass[preprint,natbib]{sigplanconf}

\usepackage{amsmath}
\usepackage{../Utils}

\begin{document}

\conferenceinfo{Haskell Symposium '08}{September 25, 2008, Victoria, British Columbia, Canada.} 
\copyrightyear{2008} 
\copyrightdata{[to be supplied]} 

\titlebanner{Draft, to be submitted to Haskell Symposium '08}        % These are ignored unless
\preprintfooter{\version}   % 'preprint' option specified.

\title{Haskell as an ADL for layered architectures}
%\subtitle{}

\authorinfo{Martijn M. Schrage \and S. Doaitse Swierstra}
           {Utrecht University}
           {\{martijn,doaitse\}@cs.uu.nl}

\maketitle

\begin{abstract}
We define a domain specific embedded language in Haskell to describe
layered software architectures of editors. By using a typed programming
language to describe the architecture, the type correctness of its components
is guaranteed by the type checker of the language. Furthermore, because the
architecture description of a system is part of the implementation of the
system, the implementation will always comply with the architecture.
\end{abstract}

\category{D.2.11} {Software Engineering}
                  {Software Architectures}
                  [Languages (e.g., description, interconnection, definition)]

\terms
Domain-specific languages, Architecture

\keywords
keyword1, keyword2



\bc
Puntjes

doc/pres/gest/upd or high/low/ehigh/elow?


Order of hor and vert is not ideal.  
f hor vert = (vert, hor) is most logical, but looks strange
f vert hor = (vert, hor) looks better but doesn't make it logical to parameterize f with its hor arg
f hor vert = (hor, vert) is chosen in thesis. result is a bit weird because vert is more logical on the left

\ec

\section{Introduction}\label{sect:introduction}

In this paper we develop a set of Haskell combinators for describing architectures of layered systems. Although designed with a layered editor in mind, the method is applicable to layered architectures in general. The combinators have been successfully used for the implementation of the generic editor Proxima~\cite{Schrage04Proxima}, as well as for a system that provides web-interfaces to databases. 

Describing an architecture in an actual programming language not only makes it possible to verify the types of the components of the architecture, but also provides a framework for implementing the system that is described by the architecture. Because the architecture description in Haskell is a program in itself, the system can be instantiated by providing implementations for each of the components.

In their survey of architecture description languages (ADLs)~\cite{medvidovic00ADLs}, Medvidovic and Taylor identify three essential components of an architecture description: a description of the (interface of the) components, a description of the connectors, and a description of the architectural configuration. They claim that the focus on {\em conceptual} architecture and explicit treatment of connectors as first-class entities differentiate ADL's from, amongst others, programming languages. However, Higher-order typed (HOT) functional programming languages, such as Haskell, Clean~\cite{plasmeijer01clean}, and ML~\cite{milner97ML} offer possibilies for describing the main components of an architecture, and treating all of these components as first-class entities. Furthermore, by using abstraction, the description of the architecture can be focused on the conceptual architecture, while the details are left to the actual components.

As argued by Hudak~\cite{hudak98DSLs}, higher-order typed functional languages offer excellent possibilities for embedding domain-specific languages. Embedding a domain-specific language facilitates reuse of syntax, semantics, implementation code, software tools, as well as look-and-feel. We give a DSEL in Haskell for describing layered editor architectures. We use records that contain functions to describe the components of the architecture. The connectors are combinators, and the configurations are programs (functions) that consist of combinators and components. 

\bc
The requirements for a DSEL for describing architectures are slightly different from the requirements for more familiar applications of DSELs, such as parsers, pretty printers, etc. The latter are used to write programs that contain many applications of the combinators and that are usually subject to frequent change. An architecture description, on the other hand, will typically be a small and rather static entity, because changes to the architecture involve changes to many components of the system. \note{is dit wel zo? misschien is concise syntax toch wel zo handig.} As a consequence, a concise syntax for combinator expressions is not of the highest importance.
\ec


Often, a program that makes use of a DSEL, contains many applications of the combinators, and hence the evaluation of the combinators forms a substantial part of the running time. Consider, for example, parsers or pretty printers. In contrast, the combinators in an architecture description are just the glue that connects the main components of the system. Most of the running time of the system can be expected to be taken up by the components and not by the combinators. Therefore, efficiency of the combinators is not a major concern. Wrapping and unwrapping a few values in the combinators is not a problem if it improves the transparency of the architecture.

In this paper we give four implementation models for layered architectures. First, we introduce a simple example layered editor architecture and explore how its main components can be modeled in Haskell. Then we proceed to connect the components. In Section~\ref{sect:simple} the connection is straightforward, with little abstraction. This is used in Section~\ref{sect:ncp} as a basis to develop a more abstract combinator implementation that uses nested pairs. In Section~\ref{sect:hidden}, we present another set of combinators, which employ a form of state hiding to improve on the previous set. Section~\ref{sect:lib} develops a small generic library for building the architecture-specific combinators of Section~\ref{sect:hidden}. 
\bc
Finally, in Section~\ref{sect:proxima}, we show how the architecture combinators are used to describe (and implement) the architecture of  the Proxima editor.%Section~\ref{sect:haskellconclusions} presents the conclusions.
\ec


%																
%																
%																
\section{A simple editor}


We introduce a simple layered architecture for a presentation-oriented editor, which is used in the next sections to explain the architecture description methods. Although the architecture is simple, it contains the essential features of the Proxima architecture. 


Figure~\ref{simpleeditprocess} depicts the editing process for our simple editor. The editor keeps track of a document (\p{doc}), which is mapped onto a presentation (\p{pres}). The presentation process is split into $n$ components: $\mathtt{present}_1 \dots \mathtt{present}_{n}$. At the bottom of the figure, the presentation is shown to a user, who provides an edit gesture (\p{gest}) in response. The edit gesture is then mapped onto a document update (\p{upd}) by $\mathtt{interpret}_{n} \dots \mathtt{interpret}_1$, after which it is performed on the document.


A layer consists of a pair of $\mathtt{present}_i$ and $\mathtt{interpret}_i$ functions, which we refer to as {\em layer functions}. Besides the vertical data flow for presentation and interpretation, we may also have horizontal data flow that stays within the layer. Horizontal data flow concerns results from a layer function that are passed to a layer function it the same layer, possibly in a next edit step.

Figure~\ref{simplesinglelayer} shows the data flow for a single layer with two examples of horizontal data flow. The \p{map} result of \p{present} is passed to \p{interpret} and represents information about the presentation mapping. Furthermore, a \p{state} parameter is passed to \p{present} as well as \p{interpret}, and may be updated by \p{interpret}. Note that the \p{state} parameter of \p{present} is the result of \p{interpret} in the previous edit step. Because of the sequential nature of the edit steps, we only consider horizontal data flow that goes from left to right.





\begin{figure}
\includegraphics[width=\columnwidth]{images/EditCycles}
\caption{Two cycles in the editing process.}\label{simpleeditprocess} 
\end{figure}



\begin{figure}
\includegraphics[width=\columnwidth]{images/SingleLayer}
\caption{A single layer.}\label{simplesinglelayer} 
\end{figure}





%																
%																
%																
\section{A layer in Haskell} \label{sect:layerInHaskell}

In the following sections, we explore the possibilities of implementing the layered architecture from the previous section in the functional language Haskell. The use of a typed programming language for describing an architecture ensures that the types of all components are correct. Furthermore, because the architecture description language is equal to the implementation language, the architecture description is an executable framework for the system being described. Thus, we eliminate the extra step of translating the architecture description to a program, which is a possibe source for errors.


There are two aspects to modeling a layered architecture in Haskell: the building blocks, which are the layer functions, and the connections between the building blocks. In the next sections, we present three methods for modeling the connections, but first we focus on the layer functions. 

A layer function takes  horizontal as well as vertical arguments and returns both  horizontal and vertical results. To make the difference between horizontal and vertical data explicit, we introduce a type synonym for layer functions.



%code Layers.hs
\begin{small}
\begin{verbatim}
type LayerFn horArgs vertArg horRess vertRes =
       horArgs -> vertArg -> (vertRes, horRess)
\end{verbatim}
\end{small}

We model a layer with a record that contains the layer functions. For the example editor, we have the two layer functions: \p{present} and \p{interpret}. The types of the layer functions follow directly from Figure~\ref{simplesinglelayer}.  If we would put these functions directly in a record, we would get:

%code Layers.hs
\begin{small}
\begin{verbatim}
data Simple state map doc pres gest upd =
       Simple { present   :: LayerFn state doc 
                                     map pres
              , interpret :: LayerFn (map,state) gest
                                     state upd
              }
\end{verbatim}
\end{small}


However, this is not entirely what we want. To simplify the horizontal connection between layer functions, we prefer a normalized data type in which the horizontal result type (\p{horRes}) of a layer function matches the horizontal argument type (\p{horArg}) of the next layer function. For our example, this implies that the horizontal result of \p{present} has the same type as the horizontal argument of \p{interpret} and vice versa (since the result of \p{interpret} is the argument of \p{present} in the next edit cycle). Figure~\ref{wrapped} shows the data flow for the layer functions in the normalized type \p{Simple}. Because the conversion to a normalized type is straightforward, we do not show it here. The definition of the normalized \p{Simple} is:

\begin{small}
\begin{verbatim}
data Simple state map doc pres gest upd =
       Simple { present   :: LayerFn state doc 
                                     (map, state) pres
              , interpret :: LayerFn (map, state) gest
                                     state upd
              }
\end{verbatim}
\end{small}

\begin{figure}
\includegraphics[width=\columnwidth]{images/NormalizedLayer}
\caption{Data flow in a normalized layer.}\label{wrapped} 
\end{figure}



%																
%																
%																
\section{Method 1: Explicitly connecting the components} \label{sect:simple}

Now that the layers have been modeled, we need to realize the data flow by connecting the layer functions. The document must be fed into the layers at the top, yielding the presentation at the bottom, and similarly, the edit gesture must be fed into the bottom layer, yielding the document update. The most straightforward way of tying everything together is to explicitly write the selection and application of each of the functions in each of the layers. 

We give an example edit loop that explicitly connects three layers: \p{layer1}, \p{layer2}, and \p{layer3} of type \p{Simple}. The data flow between the layer functions is shown in Figure~\ref{explicit}. At the bottom of the figure, the presentation is shown to the user, and an edit gesture is obtained, which we represent with two functions \p{showRendering :: Rendering -> IO ()} and \p{getGesture :: IO Gesture}. At the top of the figure, the document is updated, which we model with a function \p{updateDocument :: Update -> Document -> IO Document}. The corresponding Haskell code for the edit loop is:


\begin{figure}
\includegraphics[width=\columnwidth]{images/LayersDataFlow}
\caption{Data flow in and between layers.} \label{explicit} 
\end{figure}

%code from Explicit.hs
\begin{small}
\begin{verbatim}
editLoop (layer1, layer2, layer3) states doc = 
  loop states doc where
  loop (state1, state2, state3) doc = 
   do { let (pres1,(map1,state1')) = 
              present layer1 state1 doc
      ; let (pres2,(map2,state2')) = 
              present layer2 state2 pres1
      ; let (pres3,(map3,state3')) = 
              present layer3 state3 pres2

      ; showRendering pres3
      ; gest3 <- getGesture

      ; let (gest2,state3'')  = 
              interpret layer3 (map3, state3') gest3
      ; let (gest1,state2'')  =
              interpret layer2 (map2, state2') gest2
      ; let (update,state1'') = 
              interpret layer1 (map1, state1') gest1
      
      ; let doc' = updateDocument update doc
      ; loop (state1'', state2'', state3'') doc'
      }
\end{verbatim}
\end{small}

The following function \p{main} calls \p{editLoop} with the correct parameters.

%code from Explicit.hs
\begin{small}
\begin{verbatim}
main layer1 layer2 layer3 = 
 do { states <- initStates
    ; doc <- initDoc 
    ; editLoop (layer1, layer2, layer3) states doc
    }
\end{verbatim}
\end{small}

The functions \p{initStates} and \p{initDoc} provide the initial values for \p{states} and \p{doc}, and are left unspecified. The layers of the editor are arguments of the \p{main} function. An editor can now be instantiated by applying the function \p{main} to three \p{Simple} values that implement the different layers. The type system verifies that the implemented layer functions have the correct type signatures.

A disadvantage of the implementation of the edit loop sketched in this section is that the patterns of the data flow are not very transparent. The fact that the mapping parameters are horizontal parameters and that the presentation is a vertical parameter is not immediately clear from the program code. Moreover, explicitly encoding the standard patterns for upward and downward vertical parameters, increases the chance of errors. Finally, the number of layers is hard coded in the implementation. If the system is extended with an extra layer, variables have to be renamed. If each type appearing in the layers is distinct, the type checker catches mistakes. However, type checking will not detect two equally typed variables accidentally being swapped.


%																
%																
%																
\section{Method 2: Nested pairs}\label{sect:ncp}

\begin{figure}
\includegraphics[width=\columnwidth]{images/NestedPairs}
\caption{Horizontal parameters in nested pairs.}\label{ncp} 
\end{figure}


%\note{Ref to Doaitse Pablo?} 
In this section we abstract from the horizontal and vertical data-flow patterns in the edit loop of the previous section, by using combinators for combining layers. In the main loop, we call the layer functions of the combined layer, rather than explicitly calling each layer function in the main loop. The combinators also make the data flow more explicit. The direction of the vertical parameter is apparent by the choice of combinator, rather than explicitly threading it through the function applications.

Similar to function composition ($f \cdot g$), we develop a \p{combine} combinator that takes two layers and returns a combined layer. The layer functions of the combined layer are compositions of the layer functions in the layers that are combined.

In the method described in this section, each of the functions in the combined layer not only takes a vertical argument and returns a vertical result, but it also takes a collection of horizontal arguments (one for each layer) and returns a collection of horizontal results (one from each layer). The composition combinator takes care of distributing the horizontal arguments to the corresponding layers, and also collects the horizontal results. The combined layer provides layer functions of type \p{LayerFn horArgC vertArg horResC vertRes}. The parameters \p{horArgC} and \p{horResC} stand for the types of the collections of horizontal parameters and results. Figure~\ref{ncp} sketches the data flow in the combined layer. Only one layer function with a downward vertical parameter is shown.

Because the types of the horizontal parameters are typically not of the same type, we cannot use a list to represent the collections. Moreover, we wish to be able to determine at compile time whether the collection contains the right number of elements. A tuple or castesian product is more suitable for the task but has the disadvantage that its components cannot be accessed in a compositional way. Hence, we use a nested cartesian product consisting of only pairs to represent the horizontal parameters and results. 

Although it will not be enforced by the combinators, we only use left-associatively nested products in this section: 
\p{(\dots((e$_1$,e$_2$),e$_3$), \dots, e$_n$)}. However, as long as the structure of the argument and result products is the same, which is guaranteed by the way the \p{combine} combinator is used, the exact structure does not matter.


We first define two combinators for composing layer functions: an downward combinator \p{composeDown} (for \p{present}) and a upward combinator \p{composeUp} (for \p{interpret}). A downward vertical parameter passes through the higher layer first, whereas an upward vertical parameter passes through the lower layer first. Figure~\ref{composeDownUp} shows the data flow for the two combinators. 

The combinator \p{composeDown} composes two layers \p{higher} and \p{lower} by feeding the intermediate vertical result of \p{h} into \p{l}. At the same time, the horizontal parameters for \p{higher} and \p{lower} are taken from the horizontal parameter to the combined layer (which is a tuple), and the horizontal result for the combined layer is formed by tupling the horizontal results of \p{higher} and \p{lower}. 


%code from NCP.hs
\begin{small}
\begin{verbatim}
composeDown :: LayerFn horArgU arg horResU intr ->
               LayerFn horArgL intr horResL res ->
               LayerFn (horArgU, horArgL) arg 
                       (horResU, horResL) res
composeDown upper lower = 
  \(horArgU, horArgL) arg ->                                           
    let (interm, horResU) = upper horArgU arg
        (res, horResL)    = lower horArgL interm            
    in  (res, (horResU,horResL))
\end{verbatim}
\end{small}

\begin{figure}
\includegraphics[width=\columnwidth]{images/ComposeDown}
\includegraphics[width=\columnwidth]{images/ComposeUp}
\caption{\p{composeDown} and \p{composeUp}} \label{composeDownUp} 
\end{figure}


The definition of \p{composeUp} is analogous to \p{composeUp}. Its type is:

\begin{small}
\begin{verbatim}
composeUp :: LayerFn horArgU intr horResU res ->
             LayerFn horArgL arg horResL intr ->
             LayerFn (horArgU, horArgL) arg
                     (horResU, horResL) res
\end{verbatim}
\end{small}

\head{Combining layers}

Using \p{composeUp} and \p{composeDown}, we can define a combinator to combine \p{Simple} layers. 


First, we need to define a new data type for combined layers. Although the composition of two layer functions is a layer function itself, we cannot use the type \p{Simple} to represent a combined layer, because the types for the horizontal parameters do not match.  For \p{Simple}, the horizontal parameter of \p{present} has type \p{state}, and its result has type \p{(map, state)}. In contrast, the horizontal parameter of \p{present} in the composed layer is a nested pair of states and its result is a nested pair of \p{map} and \p{state} tuples. 

The type \p{LayerC} is a more general version of \p{Simple}. Because we cannot easily denote a nested pair structure in a Haskell type declaration, we leave the structure of the horizontal parameters unspecified. The parameter \p{states} represents the nested pair of \p{state} values, and the parameter \p{mapsStates} represents the nested pair of \p{map} and \p{state} tuples.

\begin{small}
\begin{verbatim}
data LayerC states mapsStates doc pres gest upd =
       LayerC { presentC ::   
                  LayerFn states doc mapsStates pres
              , interpretC :: 
                  LayerFn mapsStates gest states upd
              }
\end{verbatim}
\end{small}

The trivial function \p{lift} takes a layer of type \p{Simple} and returns a \p{LayerC} layer.

\begin{small}
\begin{verbatim}
lift :: Simple a b c d e f -> LayerC a (b,a) c d e f
lift simple = 
  LayerC { presentC = present simple
         , interpretC = interpret simple
         }
\end{verbatim}
\end{small}

The \p{combine} combinator is defined by using the appropriate \p{compose} combinator on each of the layer functions. 

\begin{small}
\begin{verbatim}
combine :: LayerC a b c d e f -> LayerC g h d i j e -> 
           LayerC (a,g) (b,h) c i j f
combine upper lower =
  LayerC { presentC = composeDown (presentC upper) 
                                  (presentC lower)
         , interpretC = composeUp (interpretC upper) 
                                  (interpretC lower)
         }
\end{verbatim}
\end{small}


\head{Simple editor}

The main editor loop from the previous section now reads:
 
\begin{small}
\begin{verbatim}
editLoop layers states doc = loop states doc
 where loop states doc = 
        do { let (pres, mapsStates) = 
                   presentC layers states doc
           
           ; showRendering pres
           ; gest <- getGesture
 
           ; let (update, states') = 
                   interpretC layers mapsStates gest
       
           ; let doc' = updateDocument update doc
           ; loop states' doc'
           }
\end{verbatim}
\end{small}

The \p{main} function is almost the same as in the previous section, except that instead of a 3-tuple of layers, the combined layers are passed to \p{editLoop}. 

\begin{small}
\begin{verbatim}
main layer1 layer2 layer3 = 
 do { (state1, state2, state3) <- initStates
    ; doc <- initDoc 
    ; let layers = lift layer1 `combine` lift layer2 
                               `combine` lift  layer3
    ; editLoop layers ((state1, state2), state3) doc
    }
\end{verbatim}
\end{small}

\head{Conclusions}

The nested pairs solution is more compositional than the approach of the previous section and much of the data flow is hidden from the main loop. However, the horizontal parameters are passed all the way through the composite layer, and are visible in the main loop, which is not where they conceptually belong. Moreover, the type of the composite layer is parameterized with the types appearing in the layers, leading to large type signatures.

% We therefore present yet another approach in which the horizontal parameters stay within each layer and are not visible in the
% main editor loop.




%																
%																
%																
\section{Method 3: Hidden parameters} \label{sect:hidden}

%Not fully abstracted factorized version of combinators primarily
%for explaining the model. Next section has more abstraction
%but is also harder to understand.
 
In the previous section, the horizontal results that are computed on evaluation of a combined layer function are returned explicitly and passed as arguments to the next layer function. In this section we take an alternative approach, which we explain with an example.  Recall that with the nested pairs method, each combined layer function returns a collection of horizontal results together with its vertical result:

\dots\\
{\tt  let (pres, mapsStates) = presentC layers states doc }\\
\dots\\
{\tt let (update, states') = interpretC layers mapsStates gest}\\
\dots

In contrast, the hidden-parameter method does not return a collection of horizontal results, but a function that has already been partially applied to the horizontal results.

\dots\\
{\tt let (pres, interpretStep) = presentStep doc}\\
\dots\\
{\tt let (update, presentStep') = interpretStep gest}\\
\dots

The code that is shown is not entirely accurate, as it turns out that some pattern matching is required, but it gives the general idea. Together with the presentation, \p{presentStep} returns a function \p{interpretStep} for computing the document update. The layer functions in \p{interpretStep} have already been partially applied to the horizontal results from the presentation step. Thus, the horizontal parameters are now entirely hidden from the main loop.  Thus, the main editor loop becomes more transparent and the type of a combined layer is simpler since the type variables for the horizontal parameters are hidden.


\head{Type definitions}

The type of the layer needs to have the following structure: 
\p{(Doc -> (Pres, Gest -> (Upd, Doc -> (Pres, Gest -> (Upd, \dots)))))}.  Unfortunately, we cannot use a type declaration: \p{type Layer = (Doc -> (Pres, Gest -> (Upd, Layer)))}, because Haskell does not allow recursive type synonyms. Hence, we need to use a \p{newtype} declaration, with the disadvantage that values of the type have to be wrapped with constructor functions.

\begin{small}
\begin{verbatim}
newtype Layer doc pres gest upd = 
          Layer ( doc -> 
                   ( pres, 
                       ( gest -> 
                           ( upd 
                           , Layer doc pres gest upd))))
\end{verbatim}
\end{small}

We define two combinators for constructing and combining \p{Layer} values: \p{lift} converts  a \p{Simple} layer to a hidden-parameter layer of type \p{Layer}, and \p{combine} combines two layers of type \p{Layer}. Both combinators in this section are specific to the \p{Simple} type. In the next section, we define a library to construct \p{lift} and \p{combine} for arbitrary layers.


\head{Definition of \p{lift}}

The combinator \p{lift} takes a \p{Simple} layer and returns a \p{Layer}:

\begin{small}
\begin{verbatim}
lift :: Simple state map doc pres gest upd ->
        state -> Layer doc pres gest upd
lift simple state = presStep state 
 where presStep state = Layer $
         \doc ->  let (pres, (map,state)) = 
                         present simple state doc                                         
                  in  (pres, intrStep (map,state))
       intrStep (map,state) =
         \gest -> let (upd, state') = 
                         interpret simple (map,state) gest                     
                  in  (upd, presStep state')
\end{verbatim}%$
\end{small}

Besides \p{layer}, \p{lift} gets a second parameter, \p{state}, which is the initial value of the horizontal parameter. The data flow pattern of the horizontal parameters is encoded entirely in the definition of \p{lift}. Moreover, the \p{state} type is not visible in the result of \p{lift}. Thus, once the horizontal state is passed to the lifted layer, it is no longer visible outside this layer; the \p{lift} combinator takes care of passing around the horizontal parameters to and from the layer functions, and also to the next edit cycle. 


\head{Definition of \p{combine}}

To combine layers, we define a combinator \p{combine}, which gets two layers as arguments: a higher layer and a lower layer. The type of \p{combine} is:

\begin{small}
\begin{verbatim}
combine :: Layer high med emed ehigh -> 
           Layer med low elow emed -> 
           Layer high low elow ehigh
\end{verbatim}
\end{small}

The reason for the order of the type variables is that for each pair of variables, the first type is an argument type and the second type  a result type. Hence, the first step in the combined layer is a function \p{high -> low}, which is the composition of a function \p{high -> med} in the higher layer and a function \p{med -> low} in the lower. On the other hand, the second step goes upward.  Thus, the function \p{elow -> ehigh} in the combined layer is the reverse composition of functions \p{elow -> emed} and \p{emed -> ehigh} in the higher and lower layers
  
The implementation of combine is just plumbing to get the parameters at the right places. The direction of the vertical parameters is encoded in the definition of \p{combine}. 


\begin{small}
\begin{verbatim}
combine = presStep
 where presStep (Layer upr) (Layer lwr) = Layer $ 
           \high -> let (med, uprIntr) = upr high
                        (low, lwrIntr) = lwr med
                    in  (low, intrStep uprIntr lwrIntr)
       intrStep upr lwr = 
           \elow -> let (emed, lwrPres) = lwr elow
                        (ehigh, uprPres) = upr emed
                    in  (ehigh, presStep uprPres lwrPres)
\end{verbatim}%$
\end{small}


\head{Simple editor}

The edit loop of the simple editor no longer contains references to the horizontal parameters. Furthermore, the combined layer is called \p{presentStep} instead of \p{layers} to reflect that it represents the presentation step of the computation.

\begin{small}
\begin{verbatim}
editLoop (Layer presentStep) doc = 
 do { let (pres , interpretStep) = presentStep doc
    
    ; showRendering pres
    ; gesture <- getGesture
    
    ; let (update, presentStep') = interpretStep gesture
    
    ; let doc' = updateDocument update doc
    
    ; editLoop presentStep' doc'
    }
\end{verbatim}
\end{small}

In the \p{main} function, the combined layer is created by lifting the layers together with their initial states and using \p{combine} to put them together.

\begin{small}
\begin{verbatim}
main layer1 layer2 layer3 =
 do { (state1, state2, state3) <- initStates
    ; doc <- initDoc 
    ; let layers = lift layer1 state1 `combine` 
                   lift layer2 state2 `combine`
                   lift layer3 state3
    ; editLoop layers doc
    }
\end{verbatim}
\end{small}

\head{Conclusions}

The hidden-parameter model hides the data flow of the horizontal parameters from the main loop of the system. Furthermore, the types of the horizontal parameters, as well as the intermediate vertical parameters, are hidden from the type of the composed layer. Thus, both horizontal and vertical data flow are made more transparent. 

%\note{monads arrows?} \note{relation to existential types. arrows?} 


%																
%																
%																
\section{Developing a library for architecture descriptions} \label{sect:lib}

In this section, we develop a small library for constructing \p{lift} and \p{combine} combinators for layered architectures with an arbitrary number of layer functions. We refer to these combinators as {\em meta combinators} because they are used to construct combinators.

The \p{lift} and \p{combine} combinators from the previous section are just one case of a layered architecture: a layer with two layer functions and hence two steps. Even though the combinators are straightforward, some code is duplicated, and small errors are easily made. Therefore, instead of a general description for writing \p{lift} and \p{combine} by hand, it is desirable to have a small library of meta combinators for constucting these combinators. Another advantage of a meta-combinator library is that instead of explicitly encoding the direction for each of the steps in the \p{combine} function, we can use a meta combinator whose name reflects the direction (similar to the \text{composeUp$/$Down} functions from the nested pairs method in Section~\ref{sect:ncp}).

Looking at the definitions of \p{lift} and \p{combine} in the previous section, we see that they both consist of two parts: one for each step in the layer. Both functions define a local function for each of the layer functions in the layer. In both \p{lift} and \p{combine} for the \p{Simple} layer type, these local functions are called \p{presStep} and \p{intrStep}. 

The method we use for the derivation of the meta combinators is to start with the combinators from the previous section and gradually factorize out all step-specific aspects. In the end, we have a combinator that is constructed by using straightforward applications of simple building blocks (or meta combinators). 



 
%																
\subsection{Type definitions} \label{subsecttypedef}

The \p{Layer} type poses a problem if we want to construct a library for building \p{lift} and \p{combine} functions, since somehow its constructors need to be added to and removed by the combinators. One solution is to create a type class for the constructor and deconstructor functions, but this requires a user to provide an instance of this class, as well as complicate the types and introduce problems with the monomorphism restriction. Therefore, we introduce a compositional representation of the layer types that only makes use of types defined in the library.

If we inspect the \p{Layer} type from the Section~\ref{sect:hidden}, we see it is made up of two steps of the form \p{vArg -> (vRes, ...)}. We can capture such a step with the following type:


\begin{small}
\begin{verbatim}
newtype Step a b ns = Step (a -> (b, ns))
\end{verbatim}
\end{small}

In order to compose two steps, we define an infix type constructor \p{:.:}:

\begin{small}
\begin{verbatim}
newtype (:.:) f g ns  = Comp (f (g ns))
\end{verbatim}
\end{small}

Now, for example, we can encode \p{(Doc -> (Pres, Gest -> (Upd, Doc -> (Pres, Gest -> (Upd, next)))))} as
\p{(Step Doc Pres :.: Step Gest Upd) next}. To encode the recursion, we introduce a fixed-point type \p{Fix}:

\begin{small}
\begin{verbatim}
newtype Fix f = Fix (f (Fix f))
\end{verbatim}
\end{small}

With these types, we can give a compositional point-free definition for \p{Layer}:

\begin{small}
\begin{verbatim}
type Layer doc pres gest upd = 
       Fix (Step doc pres :.: Step gest upd)
\end{verbatim}
\end{small}

Because \p{Step} and \p{:.:}  appear partially parameterized in the layer type, and \p{Fix} is recursive, all three types need to be newtypes. Hence, instead of one \p{Layer} constructor, \p{lift} and \p{combine} will be littered with constructors. This is not a problem, however, since the combinators we will derive in the next subsections take care of adding and removing these constructors.


%																
\subsection{Derivation for \p{lift}}

First we develop a meta combinator for the \p{lift} function. We start with the code for \p{lift} for a layer with two steps from Section~\ref{sect:hidden}. If we rename several variables and adapt the code for the new constructor-rich representation of the \p{Layer} type, we get:


% from Derivation.hs (remove numbers of lift and combine in source file)
\begin{small} %lift0
\begin{verbatim}
lift simple state = step1 state 
 where step1 hArg = Fix . Comp . Step $
         \vArg -> let (pres, hRes) = 
                       present simple hArg vArg
                  in  (pres, step2 hRes)
       step2 hArg = Comp . Step $
         \vArg -> let (upd, hRes) = 
                        interpret simple hArg vArg
                  in  (upd, lNilStep hRes)
       lNilStep hRes = NilStep $ step1 hRes
\end{verbatim}%$
\end{small}

The definitions of the local functions \p{step1} and \p{step2} contain mutually-recursive references. We eliminate the mutual recursion in the definitions by supplying the next step as a parameter to each function. \todo{nil}

%first drop state
%param now step1 gets step2 as param. but step2 has step1. instead of infinite
%definition lift layer = step1 (step2 (step1 ...., we make the observation that
%lift layer is step1 with the correct parameters, and hence pass lift layer as
%the parameter to step2, yielding the following finite definition:</comment></p> 
%\note{Johan: (over \p{lift layer} hieronder) "hier introduceer je een fixed point. Misschien iet meer woorden aan besteden?"} 
% ref: Augustson ICFP??
      
      
\begin{small} % lift1
\begin{verbatim}
lift :: Simple state map doc pres gest upd -> 
        state -> Layer doc pres gest upd
lift simple state =  
  step1 (step2 (lNilStep (lift simple))) state
 where step1 next hArg = Fix . Comp $ Step $
         \vArg -> let (pres, hRes) = 
                        present simple hArg vArg
                  in  (pres, next hRes)
       step2 next hArg = Comp . Step $
         \vArg -> let (upd, hRes) = 
                        interpret simple hArg vArg
                  in  (upd, next  hRes)

lNilStep next hRes = NilStep $ next hRes
\end{verbatim}
\end{small}

Now, we can capture the \p{Step} constructor and the lambda expression with the function \p{liftStep}: \todo{also Comp}

\begin{small} % lift2
\begin{verbatim}
liftStep f next horArgs = Comp . Step $ 
  \vArg -> let (vertRes, horRes) = f horArgs vArg
           in  (vertRes, next horRes)
\end{verbatim}%$
\end{small}

which yields a new \p{lift}:

\begin{small} % lift2
\begin{verbatim}
lift simple state =
  step1 (step2 (lNilStep (lift simple))) state
 where step1 next hArg = 
         Fix $ liftStep (present simple) next hArg
       step2 next hArg =
         liftStep (interpret simple) next hArg 
\end{verbatim}%$
\end{small}


If we drop the \p{state} parameter on the first two lines and rewrite the function application as a composition, we get:
\
\begin{small}
\begin{verbatim}
lift layer = (step1 . step2 . lNilStep) (lift layer)
  ...
\end{verbatim}
\end{small}

After which we can capture the recursion pattern with the \p{fix} combinator:

\begin{small}
\begin{verbatim}
fix a = let fixa = a fixa
        in  fixa
\end{verbatim}
\end{small}

Thus, we get:

\begin{small} % lift4
\begin{verbatim}
lift simple = fix (step1 . step2 . lNilStep)
 where step1 next hArg = Fix $
         liftStep (present simple) next hArg
       step2 next hArg =
         liftStep (interpret simple) next hArg
\end{verbatim}
\end{small}%$


Regardless the number of steps, there will only be one \p{Fix} constructor (in contrast to the number of \p{Comp} constructors, which depend on the number of steps). Hence, we can use a function \p{lfix} to add the constructor.

\begin{small}% lift5
\begin{verbatim}
lfix f = fix f' where f' n = Fix . (f . lNilStep) n

lift simple = lfix (step1 . step2)
 where step1 next hArg = 
         liftStep (present simple) next hArg
       step2 next hArg = 
         liftStep (interpret simple) next hArg
\end{verbatim}
\end{small}

Now we got rid of the all the constructors in the local step definitions, we can drop the \p{next} and \p{args} parameters and give a point-free definition:

\begin{small} % lift
\begin{verbatim}
lift :: Simple state map doc pres gest upd ->
        state -> Layer doc pres gest upd
lift simple = lfix $ liftStep (present simple) 
                   . liftStep (interpret simple)
\end{verbatim}%$
\end{small}


\head{\p{liftStep} works for layers with arbitrary numbers of steps}

For an $n$-step layer, the definition of \p{lift} (using the method from Section~\ref{sect:hidden}) has $n$ local step functions, each one containing a reference to the next. \bc \note{mention that the last one refers to the first?} \ec We can perform exactly the same steps as for the 2-step \p{lift}. The resulting \p{lift} contains a composition of $n$ \p{liftStep} applications. If we denote the step type constructors by \p{Step$_i$} and the layer functions by \p{layerFn$_i$}, we have:

\begin{small}
\begin{tabbing}
{\tt lift layer = } \= {\tt lfix \$ liftStep (layerFn$_1$ layer)} \\
                    \>       \dots \\
                    \> {\tt .~~~~~~liftStep (layerFn$_n$ layer)}\\
\end{tabbing}%$
\end{small}

%Thus, we can use \p{liftStep} to define the \p{lift} combinator for a layer with any number of layer functions.


%																
\subsection{Derivation for \p{combine}} \label{subsubsectcombine}


The derivation of the meta combinators for \p{combine} is largely similar to the derivation for \p{lift}. We again start with the original definition of \p{combine} for a two-step layer, and adapt to the new \p{Layer} type and rename several variables:


\begin{small} % combine0
\begin{verbatim}
combine upr lwr = step1 upr lwr
 where step1 (Fix (Comp (Step upr))) 
             (Fix (Comp (Step lwr))) = 
         Fix . Comp . Step $ 
         \high -> let (med, uprIntr) = upr high
                      (low, lwrIntr) = lwr med
                  in  (low, step2 uprIntr lwrIntr)
       step2 (Comp (Step upr)) (Comp (Step lwr)) = 
         Comp . Step $
         \low -> let (med, lwrPres) = lwr low
                     (high, uprPres) = upr med
                 in  (high, cNilStep uprPres lwrPres)
       cNilStep (NilStep u) (NilStep l) = 
         NilStep $ step1 u l 
\end{verbatim}
\end{small}

The explicit mutual recursion in the local functions is removed by passing the next step as a parameter, and rewriting the whole function as a fixed point. \todo{nil}

\begin{small} %combine2
\begin{verbatim}
combine upr lwr = fix (step1 . step2 . cNilStep) upr lwr
 where step1 next (Fix (Comp (Step upr))) 
                  (Fix (Comp (Step lwr))) = 
         Fix . Comp . Step $ 
         \high -> let (med, uprIntr) = upr high
                      (low, lwrIntr) = lwr med
                  in  (low, next uprIntr lwrIntr)
       step2 next (Comp (Step upr)) (Comp ( Step lwr)) = 
         Comp . Step $
         \low -> let (med, lwrPres) = lwr low
                     (high, uprPres) = upr med
                 in  (high, next uprPres lwrPres)

cNilStep next (NilStep u) (NilStep l) = 
  NilStep $ next u l
\end{verbatim}%$
\end{small}

Without the explicit recursive calls, we can capture the vertical data flow patterns with two functions \p{combineStepDown} and \p{combineStepUp}:

\begin{small} 
\begin{verbatim}
combineStepDown :: (f x -> g y -> h ns) -> (Step a b :.: f) x -> 
                   (Step b c :.: g) y -> (Step a c :.: h) ns
combineStepDown next (Comp (Step upper)) (Comp (Step lower)) = Comp . Step $
  \h -> let (m ,upperf) = upper h
            (l, lowerf) = lower m
        in  (l, next upperf lowerf)   


combineStepUp :: (f x -> g y -> h ns) -> (Step b c :.: f) x ->
                 (Step a b :.: g) y -> (Step a c :.: h) ns
combineStepUp next (Comp (Step upper)) (Comp (Step lower)) = Comp . Step $ 
  \l -> let (m, lowerf) = lower l
            (h, upperf) = upper m
        in  (h, next upperf lowerf)   
\end{verbatim}%$
\end{small}

rewrite . drop params step2

\begin{small}%combine3
\begin{verbatim}
combine upr lwr = fix (step1 . step2 . cNilStep) upr lwr
 where step1 next (Fix upr) (Fix lwr) = 
         Fix $ combineStepDown next upr lwr
       step2 = combineStepUp
\end{verbatim}%$
\end{small}

The second step is now simply a \p{combineStepUp}, but the first step still contains a number of constructors. In order to get rid of the \p{Fix} application, we first rewrite \p{combine} to make the pattern more apparent:

\begin{small}%combine4
\begin{verbatim}
combine = fix (\n (Fix u) (Fix l) -> 
  Fix $ (step1 . combineStepUp . cNilStep) n u l) 
 where step1 next upr lwr = 
         combineStepDown next upr lwr
\end{verbatim}
\end{small}%$

Now we can define a function \p{cfix} that pattern matches on the arguments and adds a \p{Fix} to the result:

\begin{small}
\begin{verbatim}
cfix f = fix f' 
 where f' n (Fix u) (Fix l) = Fix $ (f . cNilStep) n u l
\end{verbatim}%$
\end{small}

Which leaves us with:
\begin{small}% combine5
\begin{verbatim}
combine upr lwr = cfix (step1 . step2) upr lwr
 where step1 next upr lwr = 
         combineStepDown next upr lwr
       step2 = combineStepUp
\end{verbatim}
\end{small}


If we use \p{ccomp}, we can drop the parameters to \p{step1} and replace it by \p{combineStepDown}. Thus, the final version of \p{combine} reads

\begin{small}%combine
\begin{verbatim}
combine :: Layer high med emed ehigh ->
           Layer med low elow emed -> 
           Layer high low elow ehigh
combine = cfix (combineStepDown . combineStepUp)
\end{verbatim}
\end{small}


Similar to \p{liftStep}, \p{combineStepDown} and \p{combineStepUp} do not depend on the number of steps. Hence, they can be used to construct \p{combine} for layers with an arbitrary number of layer functions.


\head{Simple editor}

The \p{main} function for the simple editor is the same as in Section~\ref{sect:hidden}. Only the \p{editLoop} function has a couple of changes to account for the new constructors. To make the code more symmetrical, we use a deconstructor function \p{unStep :: Step a b n -> (a -> (b, ns))} (which is the selector function of \p{Step}, when declared as a record.)

\begin{small}
\begin{verbatim}
unStep (Comp (Step step)) = step
unNil (NilStep step) = step

editLoop (Fix presentStep) doc = 
 do { let (pres , interpretStep) = 
            unStep presentStep $ doc
    
    ; showRendering pres
    ; gesture <- getGesture
    
    ; let (update, presentStep') =
            unStep interpretStep $ gesture
    
    ; let doc' = updateDocument update doc
    ; 
    ; editLoop (unNil presentStep') doc'
    }
\end{verbatim}%$
\end{small}






%																
\subsection{Adding a monad}


The final modification we make to the library is to add a monad, in order to allow layer functions to perform IO operations. The type \p{LayerFn} is extended with an extra variable \p{m} for the monad.

\begin{small}
\begin{verbatim}
type LayerFn m horArgs vertArg horRess vertRes =
       horArgs -> vertArg -> m (vertRes, horRess)
\end{verbatim}
\end{small}

Consequentially, the \p{Step} type is also modified to account for the monadic result:

\begin{small}
\begin{verbatim}
newtype Step a b m ns = Step (a -> m (b, ns))
\end{verbatim}
\end{small}
\begin{small}

At composition, the monad is passed to both arguments:

\begin{verbatim}
newtype (:.:) f g m ns  = Comp (f m (g m ns))
\end{verbatim}
\end{small}

And for the fix point, the monad is passed to its argument and to the recursive call.

\begin{small}
\begin{verbatim}
newtype Fix m f = Fix (f m (Fix m f))
\end{verbatim}
\end{small}

The monadic version of the other code is largely similar to the non-monadic version. Basically, each let expression of the form

\p{\small let x$_1$ = $exp_1$; \dots ; x$_n$ = $exp_n$ in ($hRes$,$vRes$)}

is replaced by a monadic statement

\p{\small do \{ x$_1$ <- $exp_1$; \dots ; x$_n$ <- $exp_n$; return ($hRes$,$vRes$) \}}

Furthermore, the type signatures for the pairs of horizontal and vertical results \p{($hRes$, $vRes$)} become \p{m ($hRes$, $vRes$)}. Because of the similarity between the two libraries, we only show the monadic \p{liftStep}:

\begin{small}
\begin{verbatim}
liftStep :: (hArg -> vArg-> m (vRes, hRes)) -> 
            (hRes -> ns) -> hArg -> Step vArg vRes ns
liftStep f next horArgs = Step $ 
  \vArg -> do { (vertRes, horRes) <- f horArgs vArg
              ; return (vertRes, next horRes)
              }
\end{verbatim}
\end{small}%$

The functions \p{lfix}, \p{lcomp}, \p{cfix}, and \p{ccomp} are independent of the monad and are the same for both versions of the library. \todo{explain this more?}



%																
%																
%																
\section{Type class magic} \label{sect:typeClass}

type Step changes

\bc
lift simple = lfix $ liftStep (present simple) 
                   . liftStep (interpret simple)
\ec

generic lift = lfix \$  liftStep a1   . liftStep an
% $
multi parameter composition, application. 

\begin{small}
\begin{verbatim}
class Comp (comp :: * -> *) r c | comp -> r c where
  compose :: comp t -> r -> c

instance Comp (NilStep) (b->res) (b->res)  where
  compose comp r = r  

instance forall g y res cmp f a b .
         Comp g (a->res) cmp =>
         Comp (f :.: g) (y->res) ((a->y) -> cmp) where
  compose comp r = \atob -> compose (undefined :: g t')  (r . atob)
\end{verbatim}
\end{small}


\begin{small}
\begin{verbatim}
class App (comp :: * -> *) f fx r | comp f -> fx r  where
  app :: comp t -> f -> fx -> r

instance App (NilStep) (a->b) a b  where
  app comp f a = f a


instance forall g y res cmp ff f a b c d e ar rs hArg vArg hRes vRes ns dr .
         App g          (a->b)  d      e =>
         App (Step dr ar rs :.: g) (a->b) (((hRes -> g ns) -> hArg -> (Step dr vArg vRes :.: g) ns) ->d) 
                                         (LayerFn hArg vArg hRes vRes ->e) where
  
             app comp    f      fx = \x -> (app (undefined :: g t') f (fx (liftStep x))) 
\end{verbatim}
\end{small}

need to have the result type, but varying nr of params: 
\begin{small}
\begin{verbatim}
class ResType f res | f -> res where
  resType :: f -> res
  resType = undefined

instance ResType (Fix ct) (ct t)
  
instance ResType f r => ResType (a -> f) r
\end{verbatim}
\end{small}


\begin{small}
\begin{verbatim}
magicLift = app (resType magicLift) genericLift (compose (resType magicLift) id)
\end{verbatim}
\end{small}

Combine is simpeler, also a composition, but no args, so no app needed.

\begin{small}
\begin{verbatim}
class Combine (comp :: * -> *) t f | comp t -> f where
  combineC :: comp t -> f

instance Combine NilStep t ((x -> y -> t) -> (NilStep x) -> (NilStep y) -> NilStep t) where
  combineC _ = \next (NilStep x) (NilStep y) ->  NilStep (next x y) 
 
instance (Combine h t ((ft -> gt -> t) -> f ft -> g gt-> h t)) =>
         Combine (Step Down a r :.:  h) t 
                 ((ft -> gt -> t) ->
                  (Step Down a m :.: f) ft -> 
                  (Step Down m r :.: g) gt -> 
                  (Step Down a r :.: h) t) where
  combineC _ = \next f g -> combineStepDown (combineC (undefined :: h t) next) f g

instance (Combine h t ((ft -> gt -> t) -> f ft -> g gt -> h t)) =>
         Combine (Step Up a r :.:  h) t 
                 ((ft -> gt -> t) -> 
                  (Step Up m r :.: f) ft -> 
                  (Step Up a m :.: g) gt -> 
                  (Step Up a r :.: h) t) where
  combineC _ = \next f g -> combineStepUp (combineC (undefined :: h t) next) f g
\end{verbatim}
\end{small}


\begin{small}
\begin{verbatim}
magicCombine = cfix (combineC (resType magicCombine))
\end{verbatim}
\end{small}


Now everything is in the library. Just give a type and a fix and it works. main looks like this now:
\begin{small}
\begin{verbatim}
type Layer doc pres gest upd =
  Fix (Step Down doc pres  :.: Step Up gest upd :.: NilStep)

lift :: Simple state map doc pres gest upd ->
               state -> Layer doc pres gest upd
lift l = magicLift (present l) (interpret l)

main layer1 layer2 layer3 =
 do { (state1, state2, state3) <- initStates
    ; doc <- initDoc 

    ; let layers = lift layer1 state1 `magicCombine` 
                   lift layer2 state2 `magicCombine`
                   lift layer3 state3
    ; editLoop layers doc
    }
\end{verbatim}
\end{small}


%																
\subsection{Final Library and conclusions} \label{sect:libAndConclusions}

%from Derivation.hs & Layer.hs:
\begin{figure}
\begin{small}
\begin{center}
\begin{footnotesize}
\begin{verbatim}
fix :: (a->a) -> a
fix a = let fixa = a fixa
        in  fixa

type LayerFn m horArgs vertArg horRess vertRes =
       horArgs -> vertArg -> m (vertRes, horRess)

newtype Fix m f = Fix (f m (Fix m f))

newtype (:.:) f g m ns  = Comp (f m (g m ns))

newtype Step a b m ns = Step { unStep :: (a -> m (b, ns)) }


lfix f = fix f' 
 where f' n =  Fix . f n

lcomp f g = (\n s -> Comp $ f n s) . g

liftStep :: Monad m => (hArg -> vArg-> m (vRes, hRes)) -> 
            (hRes -> ns) -> hArg -> Step vArg vRes m ns
liftStep f next horArgs = Step $ 
  \vArg -> do { (vertRes, horRes) <- f horArgs vArg
              ; return (vertRes, next horRes)
              }


cfix f = fix f' 
  where f' n (Fix u) (Fix l) = Fix $ f n u l

ccomp f g = f' . g
 where f' m (Comp u) (Comp l) = Comp $ f m u l

combineStepDown :: Monad m => (nsU -> nsL -> nsC) ->
                   Step h md m nsU -> Step md l m nsL ->
                   Step h l m nsC
combineStepDown next (Step upper) (Step lower) = Step $
  \h -> do { (m ,upperf) <- upper h
           ; (l, lowerf) <- lower m
           ; return (l, next upperf lowerf)   
           }

combineStepUp :: Monad m => (nsU -> nsL -> nsC) ->
                 Step md h m nsU -> Step l md m nsL ->
                 Step l h m nsC
combineStepUp next (Step upper) (Step lower) = Step $ 
  \l -> do { (m, lowerf) <- lower l
           ; (h, upperf) <- upper m
           ; return (h, next upperf lowerf)
           }


data Simple m state map doc pres gest upd =
       Simple { present ::   LayerFn m state doc (map, state) pres
              , interpret :: LayerFn m (map, state) gest state upd
              }

type Layer m doc pres gest upd = 
       Fix m (Step doc pres :.: Step gest upd)

lift :: Monad m =>
        Simple m state map doc pres gest upd ->
        state -> Layer m doc pres gest upd
lift simple = lfix $  liftStep (present simple) 
              `lcomp` liftStep (interpret simple) 


combine :: Monad m => Layer m high med emed ehigh ->
                      Layer m med low elow emed -> 
                      Layer m high low elow ehigh
combine = cfix $ combineStepDown `ccomp` combineStepUp
\end{verbatim} %$
\end{footnotesize}\caption{Final meta combinator library}\label{fig:metacombinators} 
\end{center}
\end{small}
\end{figure}

Figure~\ref{fig:metacombinators} contains the final monadic library. In order to describe and implement an architecture, a number of definitions are necessary that depend on the number of layer functions and hence cannot be included in the library. We give a general description of these definitions.


\head{General use}
% we can define it without args or sig if -fno-mono..
% however when using combine, things go wrong.


The general case that we consider is a layer with $n$ layer functions. The record type \p{TheLayer h$_1$ \dots ~h$_m$ a$_1$ r$_1$ a$_2$ r$_2$ \dots ~a$_n$ r$_n$} contains the layer functions. The \p{h$_i$} variables are the types that appear in the horizontal parameters of the layer, and the \p{a$_i$} and \p{r$_i$} are the types of the vertical arguments and results. 

Because the types of the horizontal pararameters are not necessarily single \p{h$_i$} variables, but tuples of these variables, we denote the horizontal parameters by $horArgs_i$ and $horRess_i$. As an example, consider the type \p{Simple}. Its horizontal  type variables are \p{map} and \p{state}, but the types of the horizontal parameters are \p{state} and \p{(map, state)}. 

In general, the definition of \p{TheLayer} has this form:

\begin{small}
\begin{tabbing}
{\tt da}\={\tt ta}\\
\> {\tt Th}\={\tt eLayer h$_1$ \dots ~h$_m$ a$_1$ r$_1$ a$_2$ r$_2$ \dots ~a$_n$ r$_n$ = }\\
\> \> {\tt TheLayer~}\={\tt \{~LayerFn$_1$}\verb| :: |{\tt LayerFn~}\= {\tt $horArgs_1$ a$_1$}\\
\> \>                \>                                             \> {\tt $horArgs_2$ r$_1$}\\
\>\>\> {\tt , LayerFn$_2$}\verb| :: |{\tt LayerFn~}\={\tt $horArgs_2$ a$_n$}\\
\> \>                \>                            \>{\tt $horArgs_3$ r$_n$ \}}\\
\>\>\> {\tt \dots }\\
\>\>\> {\tt , LayerFn$_n$}\verb| :: |{\tt LayerFn~}\={\tt $horArgs_{n}$ a$_n$}\\
\> \>                \>                            \>{\tt $horArgs_1$ r$_n$ \}}\\
\end{tabbing}
\end{small}

We assume the layer is normalized, which means that $horArgs_{1} = horRess_n$ and 
$horArgs_{i+1} = horRess_i$. If the layer is not normalized, a simple wrapper function can be defined to convert the layer to a normalized layer (see Section~\ref{sect:layerInHaskell}).

\head{Type definitions}

\begin{small}
\begin{tabbing}
{\tt ty}\={\tt pe Layer a$_1$ r$_1$ a$_2$ r$_2$ \dots ~a$_n$ r$_n$ =}\\
        \>{\tt Fix (Step a$_1$ r$_1$ :.: a$_2$ r$_2$ :.: \dots ~:.: Step a$_n$ r$_n$)}\\
\end{tabbing}
\end{small}


\head{Definition of \p{lift} and \p{combine}}

The definitions of \p{lift} and \p{combine} are straightforward. For \p{lift}, we need to apply \p{liftStep} to each of the layer functions, compose the steps with \p{lcomp}, and apply \p{lfix} to the composition. 

\begin{small}
\begin{tabbing}
{\tt lift}\verb| :: |\={\tt TheLayer h$_1$ \dots ~h$_m$ a$_1$ r$_1$ \dots ~a$_n$ r$_n$ ->}\\
                     \>{\tt Layer a$_1$ r$_1$ \dots ~a$_n$ r$_n$}\\
{\tt li}\={\tt ft t}\={\tt heLayer = }\\
\>{\tt lfix \$  liftStep (LayerFn$_1$ theLayer)}\\
\>\verb|`lcomp` lift|{\tt Step (LayerFn$_2$ theLayer)}\\
\>{\tt \dots}\\ 
\>\verb|`lcomp` lift|{\tt Step (LayerFn$_n$ theLayer)}
\end{tabbing}
\end{small}%$


The \p{combine} combinator consists of $n$ \p{combineStepUp$/$Down} meta combinators, composed with \p{ccomp}, after which \p{cfix} is applied. The direction of the vertical data flow determines the choice between \p{combineStepUp} and \p{combineStepDown} for each step.  The exact type of \p{combine} depends on the direction of the meta combinators and is explained below. 

\begin{small}
\begin{tabbing}
{\tt combine}\verb| :: |{\tt Layer \dots}\verb| -> |{\tt Layer \dots}\verb| -> |{\tt Layer \dots }\\
{\tt co}\={\tt mbine}\={\tt ~= }\\
\>{\tt cfix \$~ combineStepUp$/$Down}\\
\>{\tt \dots}\\ 
\>\verb|`ccomp` combine|{\tt StepUp$/$Down}\\
\end{tabbing}%$
\end{small}

The type of \p{combine} depends on the direction of the vertical data flow in the layer. Consider the $i$-th pair of  type variables in \p{Layer a$_1$ r$_1$ \dots~a$_n$ r$_n$}. Variable \p{a$_i$} represents the vertical argument of layer function $i$, and \p{r$_i$} the vertical result. If step $i$ is an upward step, the variables at this position in the \p{Layer} types are related as follows in the type signature for \p{combine}:

\begin{small}
\begin{tabbing}
{\tt  combine :: }\={\tt Layer \dots~m h \dots}\verb| -> |{\tt Layer \dots~l m \dots} \verb| ->|\\
                  \>{\tt Layer \dots~l h \dots}\\
\end{tabbing}
\end{small}

On the other hand, for a downward layer function, we have:

\begin{small}
\begin{tabbing}
{\tt  combine :: }\={\tt Layer \dots~h m \dots}\verb| -> |{\tt Layer \dots~m l \dots} \verb| ->|\\
                  \>{\tt Layer \dots~h l \dots}
\end{tabbing}
\end{small}

\todo{show final editor code again? (only very small difference because of monad)}

\subsection{Conclusions} 

The meta combinator library has the advantages of the hidden-parameter solution from Section~\ref{sect:hidden}, but at the same time, it is much easier to describe a specific architecture. The use of meta combinators makes the data flow clearer and reduces the chance of errors in the specification. For a specific architecture, we only need to define a \p{Layer} type, and give simple definitions of \p{lift} and \p{combine}.

The fact that we still need to write some code by hand leads to the question whether it is possible to define a type class that allows us to  \p{lift} and \p{combine} as its member functions. With such a type class, and a layer type that contains an encoding of the direction of the vertical data flow, we would no longer need to explicitly define the combinators.

For \p{lift}, this type class can be defined, although it does require the explicit base case \p{TNil} for type composition (see Section~\ref{sect:decompose}). For \p{combine}, the situation is a lot more complicated due to the different data-flow directions in each of the steps. If we encode the data flow in the \p{Step} type (i.e.\ by using types \p{StepUp} and \p{StepDown}), it may be possible to define a type class for \p{combine}. Whether this is the case remains further research. \todo{Het lijkt er alleen op dat ik die klasse bijna gevonden heb, wat het hele paper in de war zou gooien..} However, it is questionable whether this is actually worth the effort. The code for the meta combinators would turn much more complex, whereas the gains of a single \p{lift} and \p{combine} function are not that much, given the fact that they only need to be defined once and will typically remain constant.


\bc Although it is possible to define the type class, it turns out to be not very useful. The generic combinator takes an argument of a certain step type and contains a recursive call to itself on the next step type. Because of this recursive call, a type constraint on the step, for example that it is an instance of \p{Wrap}, must also hold for the next step. But since the recursive call generates a constraint for its next step too, the constraint must also hold for the step after the next step, and so on. As a result, in the type of the combinator we have to explicitly give the type constraints on each of the step types. However, this means that the type of the combinator depends on the number of steps, and we do not want to encode the number of steps in the library. Therefore, we will use the simpler approach of constructing \p{lift} and \p{combine} explicitly. \ec




\bc

%																
%																
%																
\section{The Proxima editor} \label{sect:proxima}

%\todo{Check code in this section:  Swapped parameters, higher/upper , translate/interpret}

A prototype of the Proxima editor has been implemented using a monadic version of the architecture combinators of the previous section. The module that contains the architecture description is an actual part of the implementation of the prototype.


\head{The Proxima architecture}

The main difference between a Proxima layer and a \p{Simple} layer is that in Proxima, the data levels are part of the horizontal data flow. The vertical data flow consists of edit operations. Furthermore, the first step in a Proxima layer is \p{interpret} instead of \p{present}.

In Section~\ref{sect:archProximaLayers}, we explained that a layer function updates its source level. Thus, each intermediate level can be updated by two different layers: on interpretation by the layer below, and on presentation by the layer below. Thus, a data level cannot stay exclusively in one layer, but has to alternate between two layers.

bc

Figure  shows the data flow for a single Proxima layer. Note that the identifiers represent values, and not type variables. The dotted arrows outside the layer sketch the data flow of the higher and lower levels in the adjacent layers. Besides the lower-level edit operation $\delta_{Low}$, $\interpret$ gets both levels and a $state$ argument (which is explained below). The result of $\interpret$ is the higher level edit operation $\delta_{High}$, and a new $state'$. Moreover, $\interpret$ returns $low'$, which is the result of applying $\delta_{Low}$ to $Low$. The behavior of $\present$ is symmetrical.

The figure also shows how the higher level is updated by $\interpret$ of layer above, before coming back down as $high'$. Similarly, $\present$ of the layer below updates $low'$ to $low''$. 

ec

The $state$ parameter represents the local state for a layer. It should not be confused with extra state, which is part of the level. Local layer state is information that is needed by a layer, but belongs to the layer rather than to one of the adjacent levels. An example is a reference to a font metrics table in the arrangement layer.

At the top of the combined layers, the document and edit operation resulting from interpretation are passed back as arguments to the presentation step. And at the bottom, an edit gesture from the user must be put into the combined layers combination, and an updated rendering is be shown to the user. 
 

The data flow in the figure is encoded in the type definition for a Proxima layer. We denote the monad type constructor by \p{m} and the level types by \p{high} and \p{low}. For the edit operations (the $\delta$'s), we have distinct types for edit operations going up  (\p{editH} and \p{editL}) and edit operations going down (\p{editH'} and \p{editL'}). The reason for this distinction is that upward edit operations are often of a different nature than the downward ones.

\begin{small}
\begin{verbatim}
data Layer m state high low editH editL editH' editL' =
       Layer { interpret :: LayerFn m (state, high) (low, editL)
                                            (state, low) (high, editH)
              , present ::   LayerFn m (state, low) (high, editH')
                                            (state, high) (low, editL')
              }
\end{verbatim}
\end{small}

Apart from the switched directions in \p{combine} and the fact that the monadic library is used, the definitions for the Proxima architecture are virtually the same as for \p{Simple}. Moreover, the monad only affects the step type definitions and the type of the \p{lift} and \p{combine} combinators; the definitions of the combinators themselves are of the form that is described in Section~\ref{sect:libAndConclusions}. Because of the similarity, we do not reproduce the entire architecture description here.

\ec

\bc
\bigskip

The architecture combinators enforce a a strict separation between the different layers. In some cases, this . However, in general it leads to a very compositional system in which the data flow is very transparent. The addition of a new layer. Furthermore the strict separation also helped to keep user interface dependent code ... to only a few modules. Hence, adapting the system for a different user interface is not a major operation.

\bl
\o same problem as with type system. Restricting, but enforcess correctness
\o separation, all connections explicit. Very easy to add a new layer. 
\o in contrast to editors in which UI directly updates document.
\el
\ec

\bc
The motivation for using Haskell to describe editor architectures has come from the Proxima project. Proxima is a generic incremental XML editor, which is being developed at Utrecht University. The editor is parameterized with a presentation sheet that specifies how a document is presented. Furthermore, it is parameterized with a computation sheet that specifies derived values, such as chapter numbers, or a table of contents. A user can only see and edit the final presentation of the document. Because the mapping of the document on the presentation is a stepwise process, and hence the interpretation*** from edit commands on the presentation to updates on the document as well, Proxima has a layered architecture.
\ec



%																
%																
%																
\section{Related work} \label{sect:relatedWork}

Why this is nicer than an architecture description language.




%																
%																
%																
\section{Conclusions} \label{sect:conclusions}

The combinators presented in this paper make it possible to specify layered editor architectures in a concise and transparent way, making the data flow within the layer. With a small number of definitions, a layered architecture can be described, clearly showing the data flow between the layers. The combinators have been heap profiled to ensure that no memory leaks are present, and have been used to implement the Proxima prototype as well as a database web-interface system.

Because the architecture description language is embedded in the implementation language, the architecture of a system forms part of the implementation of the system. We do not need to translate the architecture to an implementation, and hence, the implementation is guaranteed to comply with the architecture.

According to Medvidovic and Taylor~\cite{medvidovic00ADLs}, an architecture description language should describe the components of an architecture, the connectors, and the configuration. For the architecture combinators defined in this paper, we can identify these aspects as follows: the layer functions are the components; \p{lift} and \p{combine} are the connectors; and the applications of \p{lift} and \p{combine} determine the configuration.

The combinator language in this paper is tailored to a specific kind of architectures: those of layered editors. Although we use the term editor in a broad sense, also including spreadsheets, e-mail agents, etc., further research should explore the possibilities of using Haskell to describe other kinds of architectures. Another area of research concerns how dynamic aspects of the architecture, such as invariants and constraints on the data, can be described and, if possible, verified.




%\acks


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\bibliographystyle{plainnat}

\bibliography{../proxima}

\end{document}

